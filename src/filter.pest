WHITESPACE = _{ " "|"\t"|"\n"|"\r" }

or = { "|" }
and = { "+" }
operator = { or | and }

gt = { ">" }
lt = { "<" }
comparator = { gt | lt }

char = { ASCII_ALPHANUMERIC | "." | "-" | "_" | "/" }

tag = { char+ }
not_tag = { "!" ~ tag }

year = { ASCII_DIGIT{4} }
year_month = { year ~ "-" ~ ASCII_DIGIT{1,2} }
year_month_day = { year_month ~ "-" ~ ASCII_DIGIT{1,2} }
date = { comparator? ~ (year_month_day | year_month | year) }

duration_designator = { "h" | "d" | "w" | "m" | "y" }
duration_length = { ASCII_DIGIT+ ~ duration_designator }
duration = { comparator? ~ duration_length }

expression = {
    SOI ~
    ((date | duration | not_tag | tag) ~ operator?)+ ~
    EOI
}
